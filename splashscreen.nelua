global ScreenState = @enum{
  DeviceSelection = 0,
  Screen1,
  Screen2,
  None
}

global SplashScreen = @record{
  timer: float32,
  state: ScreenState,
  alpha: integer,
  raylib_logo: Texture2D,
  sug_logo: Texture2D,
  logo_timer: float32,
  logo_state: integer,
  keyboard_logo: Texture2D,
  mobile_logo: Texture2D
}

function SplashScreen:init()
  self.timer = 5
  self.state = ScreenState.DeviceSelection
  self.alpha = 255
  self.raylib_logo = LoadTexture("assets/graphics/raylib_128x128.png")
  self.sug_logo = LoadTexture("assets/graphics/sug_logo2.png")
  self.keyboard_logo = LoadTexture("assets/graphics/keyboard.png")
  self.mobile_logo = LoadTexture("assets/graphics/mobile.png")
  self.logo_timer = 0
  self.logo_state = 0
end

function SplashScreen:clear()
  UnloadTexture(self.raylib_logo)
  UnloadTexture(self.sug_logo)
  UnloadTexture(self.keyboard_logo)
  UnloadTexture(self.mobile_logo)
end

function SplashScreen:update_draw()
  if (self.state == ScreenState.None) then return end

  --## if DEVICE == "desktop" then
    --local pos = GetMousePosition()
  --## elseif DEVICE == "mobile" then
  local pos = GetTouchPosition(0)
  --## end

  if (IsMouseButtonPressed(0)) then
    if (self.state == ScreenState.Screen1) then
      if (CheckCollisionPointRec(pos, Rectangle{50, 440, 480, 70})) then
        OpenURL("https://zanelittle.com")
      elseif (CheckCollisionPointRec(pos, Rectangle{50, 250, 400, 50})) then
        OpenURL("https://www.youtube.com/channel/UCuVuBcjyyZH0pqJsl1hw32Q")
      end
    end
  end

  if (self.logo_timer > 0) then
    self.logo_timer = self.logo_timer - GetFrameTime()
    if (self.logo_timer <= 0) then
      if (self.logo_state == 0) then
        self.logo_state = 1
        self.logo_timer = 0.15
      else
        self.logo_state = 0
      end
    end
  end

  if (self.timer > 0 and self.state ~= ScreenState.DeviceSelection) then
    self.timer = self.timer - GetFrameTime()
    if (self.timer < 0 and self.state == ScreenState.Screen2) then
      self.state = ScreenState.None
      self:clear()
      return
    end
  end

  switch (self.state)
  case ScreenState.Screen1 then
    if (self.timer < 0) then
      self.state = ScreenState.Screen2
    end
  case ScreenState.Screen2 then
    if (self.alpha > 0) then
      self.alpha = self.alpha - 5
    elseif (self.timer < 0) then
      self.timer = 3.5
    end
  case (ScreenState.DeviceSelection) then
    if (IsMouseButtonPressed(0)) then
      if (CheckCollisionPointRec(pos, Rectangle{90, 295, 30+MeasureText("Computer", 50), 80})) then
        DEVICE = "desktop"
        self.state = ScreenState.Screen1
        self.logo_timer = 2.5
        return
      elseif (CheckCollisionPointRec(pos, Rectangle{590, 295, 30+MeasureText("Mobile", 50), 80})) then
        DEVICE = "mobile"
        self.state = ScreenState.Screen1
        self.logo_timer = 2.5
        return
      end
    end
  end

  BeginDrawing()
  ClearBackground(WHITE)
  --DrawRectangleRec(Rectangle{230, 250, 400, 50}, BLUE)
  if (self.state == ScreenState.Screen1 or self.state == ScreenState.Screen2) then
    local black = Color{0, 0, 0, self.alpha}
    local blue = Color{0, 0, 255, self.alpha}
    local sug_color = CheckCollisionPointRec(pos, Rectangle{50, 250, 400, 50}) and blue or black
    local music_color = CheckCollisionPointRec(pos, Rectangle{50, 440, 480, 70}) and blue or black
    DrawText("Made by: SomeUnusualGames", 50, 250, 40, sug_color)
    DrawLineEx(Vector2{230, 290}, Vector2{630, 290}, 2.0, blue)
    --DrawTextureEx(self.sug_logo, Vector2{GetScreenWidth()-300, 150}, 0.0, 0.7, Color{255, 255, 255, self.alpha})
    DrawTexturePro(
      self.sug_logo,
      Rectangle{self.logo_state*300, 0, 300, 264},
      --Rectangle{GetScreenWidth()-300, 150, 210, 184.8},
      Rectangle{GetScreenWidth()-300, 150, 210, 184.8},
      Vector2{0, 0},
      0.0,
      Color{255, 255, 255, self.alpha}
    )
    DrawText("Music: ZaneLittle - https://zanelittle.com", 50, 450, 40, music_color)
    DrawLineEx(Vector2{430, 490}, Vector2{910, 490}, 2.0, blue) 
    DrawText("POWERED BY:", 200, 100, 80, Color{0, 0, 0, 255-self.alpha})
    DrawTexture(self.raylib_logo, 200, 300, Color{255, 255, 255, 255-self.alpha})
    DrawText("+ Nelua language", 350, 340, 50, Color{13, 110, 253, 255-self.alpha})
    DrawText("Player 1! Get ready!", 220, 520, 50, Color{0, 0, 0, 255-self.alpha})
  elseif (self.state == ScreenState.DeviceSelection) then
    DrawText("Please select your device to adjust the controls", 100, 100, 30, BLACK)
    DrawRectangleRec(Rectangle{90, 295, 30+MeasureText("Computer", 50), 80}, DARKGRAY)
    DrawRectangleRec(Rectangle{85, 290, 30+MeasureText("Computer", 50), 80}, GRAY)
    DrawRectangleRec(Rectangle{590, 295, 30+MeasureText("Mobile", 50), 80}, DARKGRAY)
    DrawRectangleRec(Rectangle{585, 290, 30+MeasureText("Mobile", 50), 80}, GRAY)
    DrawText("Computer", 100, 300, 50, BLACK)
    DrawTexture(self.keyboard_logo, 100, 400, WHITE)
    DrawText("Mobile", 600, 300, 50, BLACK)
    DrawTexture(self.mobile_logo, 600, 400, WHITE)
  end
  EndDrawing()
end